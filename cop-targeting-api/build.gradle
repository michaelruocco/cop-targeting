plugins {
    id "java"
    id "war"
    id "org.springframework.boot" version "2.7.5"
    id "io.spring.dependency-management" version "1.1.0"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}

springBoot {
    buildInfo()
}

ext {
    lombokVersion = "1.18.24"
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.springframework.boot:spring-boot-dependencies:2.7.5"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.apache.commons:commons-lang3:3.12.0"
}

tasks.register('processFrontendResources', Copy) {
    def frontendBuildDir = file("${project(':cop-targeting-ui').projectDir}/dist")
    def frontendResourcesDir = file("${project.buildDir}/resources/main/static")

    description 'Process frontend resources'
    dependsOn project(':cop-targeting-ui').tasks.named('assembleFrontend')

    from frontendBuildDir
    into frontendResourcesDir
}

tasks.named('processResources') {
    dependsOn tasks.named('processFrontendResources')
}

bootRun {
    systemProperties = System.properties
}